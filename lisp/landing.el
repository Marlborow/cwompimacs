(require 'seq)
(require 'recentf)
(require 'project)

(setq recentf-max-saved-items 200)
(recentf-mode 1)
(ignore-errors (recentf-load-list))

(defvar cw/global-cwd nil)
(defvar cw/global-cwd-project nil)

(defvar cwompi-landing-image "img/landing.png")
(defvar cwompi-landing-image-scale 0.6)
(defvar cwompi-landing-recents-count 5)
(defvar cwompi-landing-left-col 44)
(defvar cwompi-landing-right-col 44)
(defvar cwompi--landing-hook-installed nil)

(defvar cwompi-landing-doc-files
  '(("Documentation.org" . "Documentation.org")
    ("GithubProjects.org" . "GithubProjects.org")
    ("Books.org" . "Books.org")))

(defvar cwompi-landing-mode-map
  (let ((m (make-sparse-keymap)))
    (set-keymap-parent m special-mode-map)
    (define-key m (kbd "RET") #'cwompi-landing-activate)
    (define-key m (kbd "<return>") #'cwompi-landing-activate)
    (define-key m (kbd "<mouse-1>") #'cwompi-landing-mouse-activate)
    (define-key m (kbd "g") #'cwompi-landing-render)
    (define-key m (kbd "q") #'quit-window)
    m))

(define-derived-mode cwompi-landing-mode special-mode "Cwompi-Landing"
  (setq-local cursor-type nil)
  (setq-local truncate-lines t)
  (setq-local display-line-numbers nil)
  (when (fboundp 'display-line-numbers-mode) (display-line-numbers-mode -1))
  (when (and (boundp 'evil-mode) evil-mode)
    (when (fboundp 'evil-local-mode) (evil-local-mode 1))
    (when (fboundp 'evil-normal-state) (evil-normal-state))))

(with-eval-after-load 'evil
  (evil-set-initial-state 'cwompi-landing-mode 'normal)
  (evil-define-key 'normal cwompi-landing-mode-map (kbd "RET") #'cwompi-landing-activate)
  (evil-define-key 'normal cwompi-landing-mode-map (kbd "<return>") #'cwompi-landing-activate)
  (evil-define-key 'normal cwompi-landing-mode-map (kbd "g") #'cwompi-landing-render)
  (evil-define-key 'normal cwompi-landing-mode-map (kbd "q") #'quit-window))

(defun cwompi--landing-cols ()
  (let ((win (get-buffer-window (current-buffer) t)))
    (if (window-live-p win) (window-body-width win) (frame-width))))

(defun cwompi--startup-seconds ()
  (if (and (boundp 'after-init-time) after-init-time
           (boundp 'before-init-time) before-init-time)
      (float-time (time-subtract after-init-time before-init-time))
    0.0))

(defun cwompi--plugin-count ()
  (if (boundp 'package-activated-list)
      (length package-activated-list)
    (length features)))

(defun cwompi--recent-files ()
  (let ((files nil))
    (dolist (f recentf-list)
      (let ((ff (ignore-errors (expand-file-name f))))
        (when (and ff (file-regular-p ff) (file-readable-p ff))
          (push ff files))))
    (nreverse (delete-dups files))))

(defun cwompi--recent-dirs ()
  (let ((dirs nil))
    (dolist (f (cwompi--recent-files))
      (let ((d (file-name-directory f)))
        (when (and d (file-directory-p d))
          (push d dirs))))
    (nreverse (delete-dups dirs))))

(defun cwompi--prefer-global-cwd-first (paths)
  (let* ((gcwd (and cw/global-cwd (file-directory-p cw/global-cwd)
                   (file-name-as-directory (expand-file-name cw/global-cwd)))))
    (if (not gcwd)
        paths
      (append
       (seq-filter (lambda (p)
                     (and p (string-prefix-p gcwd (file-name-as-directory (expand-file-name p)))))
                   paths)
       (seq-remove (lambda (p)
                     (and p (string-prefix-p gcwd (file-name-as-directory (expand-file-name p)))))
                   paths)))))

(defun cwompi--apply-global-cwd (dir)
  (let* ((d (file-name-as-directory (expand-file-name dir))))
    (setq cw/global-cwd d)
    (setq cw/global-cwd-project nil)
    (setq-default default-directory d)
    (setq default-directory d)
    (when (fboundp 'cw/update-global-cwd)
      (let ((default-directory d))
        (cw/update-global-cwd)))
    (when (and cw/global-cwd (file-directory-p cw/global-cwd))
      (setq-default default-directory cw/global-cwd)
      (setq default-directory cw/global-cwd))))

(defun cwompi--open-dired-at (dir)
  (let ((d (file-name-as-directory (expand-file-name dir))))
    (cwompi--apply-global-cwd d)
    (dired d)
    (when (fboundp 'cw/update-global-cwd)
      (let ((default-directory d))
        (cw/update-global-cwd)))))

(defun cwompi--open-file-at (file)
  (let* ((f (expand-file-name file))
         (dir (file-name-as-directory (expand-file-name (file-name-directory f))))
         (gcwd (and cw/global-cwd (file-directory-p cw/global-cwd)
                    (file-name-as-directory (expand-file-name cw/global-cwd))))
         (use (if (and gcwd (string-prefix-p gcwd dir)) gcwd dir)))
    (cwompi--apply-global-cwd use)
    (find-file f)))

(defun cwompi--insert-centered-line (s &optional face)
  (let* ((w (cwompi--landing-cols))
         (tw (string-width s))
         (pad (max 0 (/ (- w tw) 2))))
    (insert (make-string pad ?\s))
    (insert (if face (propertize s 'face face) s))
    (insert "\n")))

(defun cwompi--insert-centered-image ()
  (let ((path (expand-file-name cwompi-landing-image user-emacs-directory)))
    (when (and (display-graphic-p) (file-exists-p path))
      (let* ((img (create-image path nil nil :scale cwompi-landing-image-scale))
             (px (car (image-size img t)))
             (cw (frame-char-width))
             (cols (if (and px cw (> cw 0)) (ceiling (/ (float px) cw)) 0))
             (w (cwompi--landing-cols))
             (pad (max 0 (/ (- w cols) 2))))
        (insert (make-string pad ?\s))
        (insert-image img)
        (insert "\n")))))

(defun cwompi--insert-centered-sep (len)
  (let* ((w (cwompi--landing-cols))
         (pad (max 0 (/ (- w len) 2))))
    (insert (make-string pad ?\s))
    (insert (make-string len ?-))
    (insert "\n")))

(defun cwompi--content-width ()
  (+ 1 cwompi-landing-left-col 1 cwompi-landing-right-col 1))

(defun cwompi--table-pad ()
  (let* ((w (cwompi--landing-cols))
         (content (cwompi--content-width)))
    (max 0 (/ (- w content) 2))))

(defun cwompi--table-border-top ()
  (let ((pad (cwompi--table-pad))
        (L cwompi-landing-left-col)
        (R cwompi-landing-right-col))
    (insert (make-string pad ?\s))
    (insert "┌")
    (insert (make-string L ?─))
    (insert "┬")
    (insert (make-string R ?─))
    (insert "┐\n")))

(defun cwompi--table-border-mid ()
  (let ((pad (cwompi--table-pad))
        (L cwompi-landing-left-col)
        (R cwompi-landing-right-col))
    (insert (make-string pad ?\s))
    (insert "├")
    (insert (make-string L ?─))
    (insert "┼")
    (insert (make-string R ?─))
    (insert "┤\n")))

(defun cwompi--table-border-bottom ()
  (let ((pad (cwompi--table-pad))
        (L cwompi-landing-left-col)
        (R cwompi-landing-right-col))
    (insert (make-string pad ?\s))
    (insert "└")
    (insert (make-string L ?─))
    (insert "┴")
    (insert (make-string R ?─))
    (insert "┘\n")))

(defun cwompi--truncate (s width)
  (truncate-string-to-width (or s "") width 0 ?\s))

(defun cwompi--center-in-width (s width)
  (let* ((txt (or s ""))
         (tw (string-width txt)))
    (if (>= tw width)
        (cwompi--truncate txt width)
      (let* ((pad (/ (- width tw) 2))
             (left pad)
             (right (- width tw pad)))
        (concat (make-string left ?\s) txt (make-string right ?\s))))))

(defun cwompi--insert-cell (width content)
  (cond
   ((null content)
    (insert (make-string width ?\s)))
   ((and (consp content) (eq (car content) 'button))
    (let* ((label (cwompi--truncate (nth 1 content) width))
           (path (nth 2 content))
           (kind (nth 3 content))
           (start (point)))
      (insert-text-button
       label
       'follow-link t
       'help-echo path
       'cwompi-path path
       'cwompi-kind kind
       'action #'cwompi-landing-open-at-button)
      (let* ((inserted (string-width (buffer-substring start (point))))
             (pad (max 0 (- width inserted))))
        (insert (make-string pad ?\s)))))
   (t
    (insert (cwompi--truncate content width)))))

(defun cwompi--insert-table-row (left right)
  (let ((pad (cwompi--table-pad))
        (L cwompi-landing-left-col)
        (R cwompi-landing-right-col))
    (insert (make-string pad ?\s))
    (insert "│")
    (cwompi--insert-cell L left)
    (insert "│")
    (cwompi--insert-cell R right)
    (insert "│\n")))

(defun cwompi-landing-open-at-button (button)
  (let ((p (button-get button 'cwompi-path))
        (k (button-get button 'cwompi-kind)))
    (when p
      (cond
       ((eq k 'config) (cwompi--open-dired-at user-emacs-directory))
       ((eq k 'dir) (cwompi--open-dired-at p))
       ((or (eq k 'file) (eq k 'doc)) (cwompi--open-file-at p))
       (t (cwompi--open-file-at p))))))

(defun cwompi-landing-activate ()
  (interactive)
  (let ((b (button-at (point))))
    (when b (button-activate b))))

(defun cwompi-landing-mouse-activate (event)
  (interactive "e")
  (mouse-set-point event)
  (cwompi-landing-activate))

(defun cwompi-landing-render ()
  (interactive)
  (let* ((inhibit-read-only t)
         (L cwompi-landing-left-col)
         (R cwompi-landing-right-col)
         (content (cwompi--content-width))
         (left-lines (list
                      "Cwompi's Emacs Config"
                      (format "%d plugins loaded in %.3f seconds."
                              (cwompi--plugin-count)
                              (cwompi--startup-seconds))
                      (list 'button "Configure Emacs" user-emacs-directory 'config)))
         (right-lines (append
                       (list "Cwompi's Docs")
                       (mapcar (lambda (pair)
                                 (let* ((label (car pair))
                                        (file (cdr pair))
                                        (path (expand-file-name file user-emacs-directory)))
                                   (list 'button label path 'doc)))
                               cwompi-landing-doc-files)))
         (top-rows (max (length left-lines) (length right-lines)))
         (dirs (seq-take (cwompi--prefer-global-cwd-first (cwompi--recent-dirs)) cwompi-landing-recents-count))
         (files (seq-take (cwompi--prefer-global-cwd-first (cwompi--recent-files)) cwompi-landing-recents-count))
         (rec-rows (max (length dirs) (length files))))
    (erase-buffer)
    (cwompi-landing-mode)
    (insert "\n")
    (cwompi--insert-centered-image)
    (cwompi--insert-centered-line "Cwompi-macs                                                                                " '(:height 2.0 :weight bold))
    (cwompi--table-border-top)
    (dotimes (i top-rows)
      (cwompi--insert-table-row (nth i left-lines) (nth i right-lines)))
    (cwompi--table-border-bottom)
    (insert "\n")

    (cwompi--table-border-top)
    (cwompi--insert-table-row
     (propertize (cwompi--center-in-width "Recent Directories" L) 'face '(:weight bold))
     (propertize (cwompi--center-in-width "Recent files" R) 'face '(:weight bold)))
    (cwompi--table-border-mid)
    (dotimes (i rec-rows)
      (let* ((d (nth i dirs))
             (f (nth i files))
             (dl (and d (abbreviate-file-name d)))
             (fl (and f (abbreviate-file-name f))))
        (cwompi--insert-table-row
         (and d (list 'button dl d 'dir))
         (and f (list 'button fl f 'file)))))
    (cwompi--table-border-bottom)
    (goto-char (point-min))))

(defun cwompi--landing-recenter (&rest _)
  (let ((buf (get-buffer "Cwompi-macs")))
    (when (buffer-live-p buf)
      (let ((win (get-buffer-window buf t)))
        (when (window-live-p win)
          (with-selected-window win
            (with-current-buffer buf
              (cwompi-landing-render))))))))

(defun cwompi-landing-buffer ()
  (let ((buf (get-buffer-create "Cwompi-macs")))
    (with-current-buffer buf
      (cwompi-landing-render)
      (add-hook 'window-configuration-change-hook #'cwompi--landing-recenter nil t))
    (unless cwompi--landing-hook-installed
      (add-hook 'window-size-change-functions #'cwompi--landing-recenter)
      (setq cwompi--landing-hook-installed t))
    buf))

(defun cwompi-landing ()
  (interactive)
  (switch-to-buffer (cwompi-landing-buffer))
  (cwompi--landing-recenter))

(setq initial-buffer-choice #'cwompi-landing-buffer)
(provide 'landing)
